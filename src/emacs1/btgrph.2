
;;; BBN Bitgraph terminal.  Like an ANSI VT100 with insert/delete
;;; line, char I/D, and scrolling.

IFN BITGRA,[				;[FHSU] FOR V2.0+ BITGRAPH
BBNTB:	63.,,84.
	%TOERS+%TOMVB+%TOMVU+%TOLWR+%TOLID+%TOCID,,%TPRSC
	CALL VT1CPS
	CALL VT1CEL
	CALL VT1CES
	CALL VT1CLR
	JFCL
	CALL VT1DSM
	CALL VT1DMV
	CALL AMINSL		;[fHsu] THESE NEVER GET CALLED
	CALL AMDELL		;[fHsu] IF SCROLLING IS ON.
	CALL AMINSC		;[fHsu] INS CHAR
	CALL AMDELC		;[fHsu] DEL CHAR
	CALL BBNRST		;[fHsu] RESET
	CALL BBNSUP		;[fHsu] SCROLL UP
	CALL BBNSDN		;[fHsu] SCROLL DOWN
	JFCL			;[fHsu] INIT
	JFCL			;[fHsu] RESET TEMPORARILY
	CALL AMINV		;SET/RESET INVERSE VIDEO MODE
	CALL AMCINV

BBNRST:	HRROI A,[ASCIZ /[1;64r[64;1H/]	;RESET SCROLL REGION and PUT CURSOR
						;AT BOTTOM OF SCREEN.
	PSOUT
	RET

;SCROLL Q LINES STARTING WITH LINE IN BP UP
BBNSUP:	PUSH P,["D]	;INDEX NEEDED TO CAUSE SCROLLING (DOESNT TAKE ARGUMENT)
	JSP A,BBNSCR	;SETUP SCROLL REGION AND BYTE POINTER IN A
	MOVE B,BOTLIN	;POSITION TO BOTTOM OF SCROLL REGION, OFFSET
BBNUP2:	CALL VT1ARG
	MOVEI B,"H
	IDPB B,A
	MOVEI B,0	;MAKE ASCIZ
	IDPB B,A
	HRROI A,BBNBUF	;GET COMMANDS BACK
	PSOUT		;SET SCROLLING REGION
	MOVEI A,(Q)	;CALCULATE PADDING
	IDIVI A,10.	;CHANGE THIS AS NEEDED
	MOVEM A,BBNPAD	;SAVE PAD CALCULATION
BBNUP3:	MOVE A,-2(P)	;GET CURSOR COMMAND - INDEX OR REVERSE INDEX
	CALL OUTESC	;OUTPUT IT
	MOVEI A,0	;BG PAD CHARACTER - DONT USE DELS!
	MOVE B,BBNPAD	;GET BACK PADDING
BBNLUP:	PBOUT
	SOJG B,BBNLUP
	SOJG Q,BBNUP3
	JRST POPCBA	;ALSO FLUSH SCROLLING COMMAND

BBNSDN:	PUSH P,["M]	;REVERSE INDEX TO SCROLL BACKWARDS
	JSP A,BBNSCR	;SETUP SCROLL REGION
	MOVEI B,1(BP)	;MOVE TO TOP LINE, OFFSET
	JRST BBNUP2

BBNSCR:	PUSH P,B
	PUSH P,C
	PUSH P,A	;SAVE RETURN ADDRESS AS WELL
	MOVE A,[440700,,BBNBUF]	;MAKE STRING POINTER
BBNSC1:	MOVEI B,1(BP)	;STARTING LINE, OFFSET
	CALL VT1ARG
	MOVEI B,";
	IDPB B,A
	MOVE B,BOTLIN	;BOTTOM LINE, OFFSET
	CALL VT1AR1
	MOVEI B,"r	;SET SCROLL REGION
	IDPB B,A
	RET

]  ;IFN BITGRA
